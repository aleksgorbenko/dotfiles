# zmodload zsh/zprof # uncomment to profile laoding speed

# shortcut to this dotfiles path is $ZSH
export ZSH=${HOME}/.dotfiles
# your project folder that we can `c [tab]` to
export PROJECTS=${HOME}/projects

# 1. Load environmental variables
# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
load_local_env_vars () {
	if [[ -a ${HOME}/.localrc ]]
	then
		source ${HOME}/.localrc
	fi
}

# 2. Load aliases
load_aliases () {



	# Keep your localised aliases in the ~/.aliases.
	# These are relevant for the local machine only (work or neighbours laptop)
	if [[ -a ${HOME}/.aliases ]]
	then
		source ${HOME}/.aliases
	fi
}

# 3. Load all zsh files in the current directory
load_zsh_config_files () {
	typeset -U config_files
	config_files=(${ZSH}/**/*.zsh)

	# 3.1 load PATH
	source ./path.zsh

	# 3.2 Load everything but the path/completion/hooks files
	for file in ${${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/pre-commit.zsh}:#*/post-merge.zsh}
	do
		source $file
	done
}

# 4. Load all completion functionality
load_autocompletion() {
	# 4.1 init completion (check completion cache once a day)
	autoload -Uz compinit
	if [ $(date +'%j') != $(stat -f '%Sm' -t '%j' ${HOME}/.zcompdump) ]; then
		compinit
	else
		compinit -C
	fi

	typeset -U config_files
	config_files=(${ZSH}/**/*.zsh)
	# 4.2 Load every completion config after autocomplete loads
	for file in ${(M)config_files:#*/completion.zsh}; do
		source $file
	done
}

# 5. Helpful packages
load_extras () {
	# 5.1 numbered folder and git output
	if [[ -x /usr/local/bin/scmpuff ]]; then
		eval "$(scmpuff init -s)"
	fi

	# 5.2 better navigation
	if [[ -e "/usr/local/etc/profile.d/autojump.sh" ]]; then
		source "/usr/local/etc/profile.d/autojump.sh"
	fi

	# 5.3 shell integration
	if [[ -e "${HOME}/.iterm2_shell_integration.zsh" ]]; then
		source "${HOME}/.iterm2_shell_integration.zsh"
	fi

	# 5.4 fuzzy finder helpers
	if [[ -f ~/.fzf.zsh ]]; then
	 source ~/.fzf.zsh
	fi

	# 5.5 colored output for common unix commands
	[[ -s "/etc/grc.zsh" ]] && source /etc/grc.zsh
}

# 6. Plugins
# We are using antibody plugin manager
# Add new plugins to the `zsh_plugins.symlink` file and relaod zsh
# Keep syntax highlighting plugin last
load_plugins () {
	antibody bundle < "${HOME}/.zsh_plugins" > "${ZSH}/zsh/zsh_plugins.sh"
	source "${ZSH}/zsh/zsh_plugins.sh"
}

# DO NOT CHANGE THE SEQUENCE!
load_local_env_vars
load_aliases
load_zsh_config_files
load_autocompletion
load_extras
load_plugins

# zprof # uncomment to profile laoding speed
source "${ZSH}/zsh/config.zsh"
rbenv
