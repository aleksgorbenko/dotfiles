# zmodload zsh/zprof # uncomment to profile laoding speed

# shortcut to this dotfiles path is $ZSH
export ZSH=${HOME}/.dotfiles
# your project folder that we can `c [tab]` to
export PROJECTS=${HOME}/projects

# 1. Load environmental variables
# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
load_local_env_vars () {
	if [[ -a ${HOME}/.localrc ]]
	then
		source ${HOME}/.localrc
	fi
}

# 2. Load local aliases
# Keep your localised aliases in the ~/.aliases.
# These are relevant for the local machine only (work or neighbours laptop)
load_local_aliases () {
	if [[ -a ${HOME}/.aliases ]]
	then
		source ${HOME}/.aliases
	fi
}

# 3. Load all zsh files in the current directory
load_zsh_config_files () {
	typeset -U config_files
	config_files=(${ZSH}/**/*.zsh)

	# 3.1 load all path.zsh files
	for file in ${(M)config_files:#*/path.zsh}; do
		source $file
	done

	# 3.2 Load everything but the path/completion/hooks files
	for file in ${${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/pre-commit.zsh}:#*/post-merge.zsh}
	do
		source $file
	done
}

# 4. Load all completion functionality
load_autocompletion() {
	# 4.1 init completion (check completion cache once a day)
	autoload -Uz compinit
	if [ $(date +'%j') != $(stat -f '%Sm' -t '%j' ${HOME}/.zcompdump) ]; then
		compinit
	else
		compinit -C
	fi

	typeset -U config_files
	config_files=(${ZSH}/**/*.zsh)
	# 4.2 Load every completion config after autocomplete loads
	for file in ${(M)config_files:#*/completion.zsh}; do
		source $file
	done
}

# 5. Helpful packages
load_extras () {
	# 5.1 numbered folder and git output
	if [[ -f "${HOME}/.scm_breeze/scm_breeze.sh" ]]; then
		source "${HOME}/.scm_breeze/scm_breeze.sh"
	fi

	# 5.2 better navigation
	if [[ -f "/usr/local/etc/profile.d/autojump.sh" ]]; then
		source "/usr/local/etc/profile.d/autojump.sh"
	fi

	test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"
	eval $(thefuck --alias)
}

# 6. Plugins
# We are using antibody plugin manager
# Add new plugins to the `zsh_plugins.symlink` file and relaod zsh
# Keep syntax highlighting plugin last
load_plugins () {
	antibody bundle < "${HOME}/.zsh_plugins" > "${ZSH}/zsh/zsh_plugins.sh"
	source "${ZSH}/zsh/zsh_plugins.sh"
}

# DO NOT CHANGE THE SEQUENCE!
load_local_env_vars
load_local_aliases
load_zsh_config_files
load_autocompletion
load_extras
load_plugins

# zprof # uncomment to profile laoding speed
